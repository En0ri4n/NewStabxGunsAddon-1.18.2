buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = "${mod_version}"
group = "com.stabilizerking.stabxmodernguns"
archivesBaseName = "Stabx_Modern_Guns"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: "${mappings_channel}", version: "${parchment_version}"

    runs {

        configureEach {
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.disableRefMap', 'true'

            mods {
                stabxmodernguns {
                    source sourceSets.main
                }
            }
        }

        client {
            workingDirectory project.file('run')
        }

        server {
            workingDirectory project.file('run/SERVER')
            arg '-nogui'
        }

        data {
            workingDirectory project.file('run')
            args '--mod', "${mod_id}", '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

repositories {
    maven {
        url = "https://www.cursemaven.com"
    }

    mavenLocal()
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"

    implementation fg.deobf('curse.maven:framework-549225:4287475')
    //implementation fg.deobf('curse.maven:mrcrayfishs-gun-mod-289479:4344223')
    implementation fg.deobf('com.mrcrayfish:NewMrCrayfishGunMod:1.3.3-modified-1.18.2')
    runtimeOnly fg.deobf('curse.maven:catalogue-459701:3803098')
    runtimeOnly fg.deobf('curse.maven:jei-238222:3940240')
}

jar {
    manifest {
        attributes([
                "Specification-Title": "${mod_name}",
                "Specification-Vendor": "Stabilizer King (modified by En0ri4n)",
                "Specification-Version": "${mod_version}",
                "Implementation-Title": project.name,
                "Implementation-Version": "${mod_version}",
                "Implementation-Vendor" :"Stabilizer King (modified by En0ri4n)",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.register('sourceJar', Jar) {
    dependsOn tasks.classes
    from sourceSets.main.allSource
    classifier 'sources'
    extension 'jar'
    group 'build'
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifacts {
                artifact jar
                artifact tasks.sourceJar
            }
        }
    }
    repositories {
        maven {
            url "file:///X:/localmaven/mcmods"
        }
    }
}